(ns kotoba.native.vector-region-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.native.aarch64 :as aarch64]
            [kotoba.native.machine-ir :as machine]
            [kotoba.native.vector-region :as region]
            [kotoba.native.x86-64 :as x86]))

(defn runtime-calls [form]
  (filter #(= :gmir/runtime-call (:gmir/op %))
          (:gmir/instructions (machine/lower-kir-expression [] form))))

(deftest bounded-let-vector-is-lowered-to-a-scalar-region
  (let [source '(let [v (vector-new (+ 1 2) (+ 3 4))]
                  (+ (vector-at v 0) (vector-count v)))
        lowered (region/rewrite-expression source)]
    (is (empty? (runtime-calls lowered)) (pr-str lowered))
    (is (= 1 (count (filter #{'(+ 1 2)} (tree-seq coll? seq lowered)))))
    (is (= 1 (count (filter #{'(+ 3 4)} (tree-seq coll? seq lowered)))))
    (is (not-any? #{'vector-new 'vector-at 'vector-count}
                  (tree-seq coll? seq lowered)))))

(deftest immediate-vector-read-and-count-also-stay-in-the-region
  (doseq [source ['(vector-count (vector-new (+ 1 2) (+ 3 4)))
                  '(vector-at (vector-f64-new 1 2) 1)]]
    (let [lowered (region/rewrite-expression source)]
      (is (empty? (runtime-calls lowered)) (pr-str lowered))
      (is (not-any? #{'vector-new 'vector-f64-new 'vector-at 'vector-count}
                    (tree-seq coll? seq lowered))))))

(deftest dynamic-read-is-bounds-checked-and-evaluated-once
  (let [lowered (region/rewrite-expression
                 '(let [v (vector-new 11 22)] (vector-at v (+ 0 1))))]
    (is (empty? (runtime-calls lowered)))
    (is (= 1 (count (filter #(= '(+ 0 1) %)
                            (tree-seq coll? seq lowered)))))
    (is (some #{'(quot 1 0)} (tree-seq coll? seq lowered)))))

(deftest escape-and-unsupported-use-retain-host-representation
  (doseq [source ['(let [v (vector-new 1 2)] v)
                  '(let [v (vector-new 1 2)] (vector-conj v 3))
                  '(let [v (vector-new 1 2)] (f v))]]
    (let [lowered (region/rewrite-expression source)]
      (is (some #{'vector-new} (tree-seq coll? seq lowered)) (pr-str source)))))

(deftest lexical-shadowing-does-not-consume-the-outer-region
  (let [source '(let [v (vector-new 1 2)]
                  (+ (vector-at v 0) (let [v 9] v)))
        lowered (region/rewrite-expression source)]
    (is (empty? (runtime-calls lowered)))
    (is (some #{9} (tree-seq coll? seq lowered)))))

(deftest both-native-emitters-consume-the-region-rewrite
  (let [kir {:format :kotoba.kir/v4
             :exports ['main]
             :functions [{:name 'main :params [] :param-types [] :result :i64
                          :body '(let [v (vector-new 7 8 9)]
                                   (+ (vector-at v 1) (vector-count v)))}]}]
    (is (seq (:code (x86/emit-program kir))))
    (is (seq (:code (aarch64/emit-program kir))))))
