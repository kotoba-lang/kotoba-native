(ns kotoba.native.peephole-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.native.peephole :as peephole]))

(deftest constant-operand-recognizes-exactly-the-safe-forms
  (is (= 7 (peephole/constant-operand 7)))
  (is (= -1 (peephole/constant-operand -1)))
  (is (= 0 (peephole/constant-operand 0)))
  (testing "booleans are the i64 words one and zero"
    (is (= 1 (peephole/constant-operand true)))
    (is (= 0 (peephole/constant-operand false))))
  (testing "false and zero are PRESENT, not absent -- callers must use some?"
    (is (some? (peephole/constant-operand false)))
    (is (some? (peephole/constant-operand 0))))
  (testing "forms that read a stack slot or clobber state are refused"
    (doseq [form ['x "s" '(f 1) '(cap-call 3 1) '(+ 1 2) '(let [a 1] a) nil]]
      (is (nil? (peephole/constant-operand form))))))
